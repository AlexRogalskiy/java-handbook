buildscript {
    ext {
        springBootVersion = '1.4.1.RELEASE'
    }
    repositories {
        mavenCentral()
        maven {
            url 'http://repo.springsource.org/milestone/'
        }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath 'io.spring.gradle:dependency-management-plugin:0.6.0.RELEASE'
    }
}

apply plugin: 'java'
apply plugin: 'idea'

group = 'com.example'
version = '0.0.1-SNAPSHOT'

description = """java handbook sample code"""

sourceCompatibility = 1.8
targetCompatibility = 1.8

// https://github.com/spring-io/platform/blob/master/platform-bom/pom.xml
// https://github.com/spring-projects/spring-boot/blob/master/spring-boot-dependencies/pom.xml

allprojects {
    apply plugin: 'spring-boot'
    apply plugin: 'io.spring.dependency-management'

    repositories {
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
        maven { url "http://repo.maven.apache.org/maven2" }
    }
    
    ext['guava.version'] = '19.0' // Update from Spring IO default "17.0" (by Athens-RELEASE)
    ext['okhttp3.version'] = '3.4.1' // Update from Spring IO default "3.3.1" (by Athens-RELEASE)
    ext['assetj.version'] = '3.5.2' // Update from Spring IO default "2.5.0" (by Athens-RELEASE)

    dependencyManagement {
        imports {
            mavenBom 'io.spring.platform:platform-bom:Athens-RELEASE'
        }

        dependencies {
            dependency 'org.mybatis.spring.boot:mybatis-spring-boot-starter:1.1.1'
            dependency 'org.mybatis:mybatis-typehandlers-jsr310:1.0.1'
            dependency 'org.webjars.npm:bootstrap:3.3.5'
            dependency "com.squareup.okhttp3:mockwebserver:" + ext['okhttp3.version']
            dependency 'com.github.jbellis:jamm:0.3.1'
        }
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation" << '-parameters'
}

configurations {
    compile.exclude module: "spring-boot-starter-tomcat"
    compile.exclude module: "commons-logging"
}

dependencies {
    compile 'com.github.jbellis:jamm'
    compile 'com.google.guava:guava'
    compile 'com.h2database:h2'
    compile 'mysql:mysql-connector-java'
    compile 'org.mybatis:mybatis-typehandlers-jsr310'
    compile 'org.mybatis.spring.boot:mybatis-spring-boot-starter'
    compile 'org.projectlombok:lombok'
    compile 'org.springframework.boot:spring-boot-starter-freemarker'
    compile 'org.springframework.boot:spring-boot-starter-jdbc'
    compile 'org.springframework.boot:spring-boot-starter-jetty'
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.webjars.npm:bootstrap'

    testCompile 'com.squareup.okhttp3:mockwebserver'
    testCompile 'org.assertj:assertj-core'
    testCompile 'org.mockito:mockito-core'
    testCompile 'org.springframework.boot:spring-boot-starter-test'
}
