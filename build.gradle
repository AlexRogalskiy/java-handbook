buildscript {
    ext {
        springBootVersion = '1.4.1.RELEASE'
    }
    repositories {
        mavenCentral()
        maven {
            url 'https://repo.springsource.org/milestone/'
        }
        maven { url 'https://repo.spring.io/plugins-release' }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath 'io.spring.gradle:dependency-management-plugin:0.6.0.RELEASE'
        classpath 'io.spring.gradle:propdeps-plugin:0.0.9.RELEASE'
    }
}

apply plugin: 'java'
apply plugin: 'idea'

group = 'com.example'
version = '0.0.1-SNAPSHOT'

description = """java handbook sample code"""

sourceCompatibility = 1.8
targetCompatibility = 1.8

// https://github.com/spring-io/platform/blob/master/platform-bom/pom.xml
// https://github.com/spring-projects/spring-boot/blob/master/spring-boot-dependencies/pom.xml

allprojects {
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java'
    apply plugin: 'propdeps'
    apply plugin: 'propdeps-maven'
    apply plugin: 'propdeps-idea'
    apply plugin: 'propdeps-eclipse'

    repositories {
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
        maven { url "https://repo.maven.apache.org/maven2" }
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation" << '-parameters'
    }
compileJava {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation" << '-parameters'
}

    ext['guava.version'] = '19.0' // Update from Spring IO default "17.0" (by Athens-RELEASE)
    ext['okhttp3.version'] = '3.4.1' // Update from Spring IO default "3.3.1" (by Athens-RELEASE)
    ext['assetj.version'] = '3.5.2' // Update from Spring IO default "2.5.0" (by Athens-RELEASE)
    ext['lombok.version'] = '1.16.10'

    dependencyManagement {
        imports {
            mavenBom 'io.spring.platform:platform-bom:Athens-RELEASE'
        }

        dependencies {
            dependency 'org.mybatis.spring.boot:mybatis-spring-boot-starter:1.1.1'
            dependency 'org.mybatis:mybatis-typehandlers-jsr310:1.0.1'
            dependency 'org.webjars.npm:bootstrap:3.3.5'
            dependency "com.squareup.okhttp3:mockwebserver:" + ext['okhttp3.version']
            dependency 'com.github.jbellis:jamm:0.3.1'
            dependency "org.projectlombok:lombok:${ext['lombok.version']}"
        }
    }

    dependencies {
        optional 'org.projectlombok:lombok'
    }
}

configurations {
    compile.exclude module: "spring-boot-starter-tomcat"
    compile.exclude module: "commons-logging"
}
